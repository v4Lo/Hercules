//===== Hercules Script ======================================
//= Kafra Functions
//===== By: ==================================================
//= rAthena Dev Team
//= Darlskies, Darkchild, Syrus22, Lupus, kobra_k88 (2.0)
//= L0ne_W0lf
//===== Current Version: =====================================
//= 6.7
//===== Description: =========================================
//= These functions handle save, storage, cart rental, teleport,
//= and Free Teleport/Warp/Rent Cart options for all Kafra NPCs.
//=
//= Kafra's will need a lot of work. The Welcome Message argument
//= may become obsolete, as most kafras have a slightly differing
//= Welcome message.
//===== Additional Comments: =================================
//= 1.1 Now using functions v2.1 Added Cart Rent for Classes: Whitesmith, Professor.
//=     Replaced checkoption(x) into checkcart(0) [Lupus] v2.1b Added Fix Kafra Pass Func [Kobra_k88]
//= 2.2 Final fix of the Kafra Pass Exploit! [Lupus] a -Izlude[4] fix
//= 2.2a Minor changes to function calls.  Using agruments. Added Guild options. [kobra_k88]
//= 2.2b This version uses arrays for the teleport option. Rearranged next statements to make menu transitions smoother. [kobra_k88]
//= 2.3 Removed SAVE from Niflheim. [Lupus]
//= 2.3 removed "fix" by HawkMoon RTFM and check supernovice.txt . There's a SPECIAL Kafra which gives CARTS to SN for a special proce. [Lupus]
//= 2.4 Added Baby Class Support (Baby Novice check) Removed annoying storage feature where u had to close dialog window to be able to use your storage [Lupus]
//= 2.5 Added Louyang official Kafra, fixes some Kafras, not letting you to Save your position [Lupus]
//= 2.6 Reverted Dungeons Kafras (they should offer only Storage). Added temp Ayothaya Kafra
//= 2.7 Added correct Ayothaya, Louyang & Amatsu Kafras. [Lupus]
//= 2.8 Fixed Amatsu Storage problems [Lupus] 2.9 Fixed spelling mistakes. [Nexon]
//= 3.0 Added special "not working teleport menu" for Einbroch Kafras [Lupus]
//= 3.1 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 3.2 Fixed an exploit [Lupus]
//= 4.0 Added Kafra storage password protection. [Lupus]
//=     to block Kafra Password, read comments at 350 line
//= 5.0 Fixed the close2;....close; end; lines. Who let them slip away? =/ [erKURITA]
//=     Also, the kafra upon warp was giving back the zeny. Removed. [erKURITA]
//= 5.1 Optimized a little, added no tele/save arg's [Evera]
//= 5.1a Temporary? Added F_ClearGarbage to clear unused/outdated variables [Lupus]
//= 5.2 By default commented out custom Guilds Kafra's "Guild Storage". [Lupus]
//= 5.3 uncommented Guild Storage. Confirmed kRO feature. [Lupus]
//= 5.4 added -Guide option (Kafra shows you nearest Guide) Work in progress.
//=     Need all coords for all guides.. and somehow pass their coords to kafra.
//=     Removed universal Kafra Pass, added 3 new Kafra Tickets [Lupus]
//= 5.4b temporary moved "-Guide" from the 1st menu punct [Lupus]
//= 5.5 Added proper Niflheim welcome message.	[L0ne_W0lf]
//=     Cleaned up the menus a bit. Got rid of the proceeding "-" prefix.
//= 5.5b Missed a preceeding hyphen, which was screwing up teleporting. [L0ne_W0lf]
//= 5.6 Further dialog updates, added  dynamic costs for cart and storage use. [L0ne_W0lf]
//=     Updated some functions to handle Guild Kafras the way they should.
//= 5.6a Corrected a few wrong names in the teleport function. [L0ne_W0lf]
//= 5.7 Correced end message no longer displaying, and the duplicate names [L0ne_W0lf]
//=     Thanks to Barron-Monster for pointing out the issues.
//= 5.8 Fixed another double name being shown when "saving" [L0ne_W0lf]
//= 5.9 Fixed the Kafra Welcome message for guilds. Thanks Barron-Monster. [L0ne_W0lf]
//= 6.0 Closed Kafra Password exploit in Cool Event Corp. Storages until we got a client/packet based password support.
//=     Added 2 args into F_SetKafCode to fit it in Cool Evnt Co. NPC [Lupus]
//= 6.1 Added menu for Turbo Track Kafra Staff. [L0ne_W0lf]
//= 6.2 Updated/Fixed warp cords. [Kisuka]
//= 6.3 #kafra_code is now stored as is. [brianluau]
//= 6.4 Removed unofficial Guide function. [Daegaladh]
//= 6.5 Optimized. [Joseph]
//= 6.6 Added Warp Points / View Points Function. [Joseph]
//= 6.7 Added Rune Knight Kafra. [Euphy]
//============================================================

//=== Main Function ========================================================
//=   arg(0): Used to determine which welcome message to show.
//=   arg(1): Used to determine which menu to display.
//=   arg(2): Used to determine if the info menu is shown in F_KafInfo.
//=   arg(3): Cost to use Storage
//=   arg(4): Cost to Rent a Pushcart
//==========================================================================
function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; // Clear outdated, unused variables
	// Display Kafra Welcome Message
	switch(getarg(0)){
		default:
		case 0:
			// Default message (obsolete)
			mes "[Kafra Employee]";
			mes "Welcome to the";
			mes "Kafra Corporation.";
			mes "The Kafra services";
			mes "are always on your side.";
			mes "How may I assist you?";
			break;
		case 1:
			// Niflheim specific message
			mes "[Kafra Employee]";
			mes "^666666W-weeeelc-c-come";
			mes "to th-the K-kaaafrrrra";
			mes "C-coorpoor-r-ratioooonn...^000000";
			break;
		case 2:
			// Guild Castle Kafra message
			set @GID,getcharid(2);
			mes "[Kafra Employee]";
			mes "Welcome. ^ff0000" + getguildname(@GID) + "^000000 Member.";
			mes "The Kafra Coporation will stay with you wherever you go.";
			break;
		case 3:
			// Amatsu specific message (obsolete)
			mes "[Kafra Employee]";
			mes "So, have you come from a faraway land to study our culture, or are you just sightseeing?";
			mes "In either case, why not stay awhile?";
			mes "The air is eternally heavy with the";
			mes "scent of pleasant wildflowers.";
			break;
		case 4:
			// Louyang and Ayothaya specific message (obsolete)
			mes "[Kafra Employee]";
			mes "With our many Kafra";
			mes "service locations, you're never";
			mes "far from home.";
			break;
		case 5:
			// NPC has it's own welcome message. (Display nothing)
			break;
	}
	next;
	while (1) {
		deletearray @K_Menu0$[0],getarraysize(@K_Menu0$);
		if (getarg(0) == 2) {
			// Guild Kafra Menu override (free Teleport, free Storage, Free Cart)
			setarray @K_Menu0$[0],"Use Storage","Use Guild Storage","Rent a Pushcart","Use Teleport Service","Cancel";
		} else {
			switch(getarg(1)){
				// Save and Storage only
				case 1:	setarray @K_Menu0$[0],"Save","Use Storage","Use Master Storage","Cancel"; break;
				// Storage only
				case 2:	setarray @K_Menu0$[0],"Use Storage","Use Master Storage","Cancel"; break;
				// No Teleport (Common)
				case 3: setarray @K_Menu0$[0],"Save","Use Storage","Use Master Storage","Rent a Pushcart","Check Other Information","Cancel"; break;
				// Case 4 is Einbroch no tele message.
				// No save, or teleport. (Common)
				case 5:	setarray @K_Menu0$[0],"Use Storage","Use Master Storage","Rent a Pushcart","Check Other Information","Cancel"; break;
				// Storage and Check Other Information only.
				case 6:	setarray @K_Menu0$[0],"Use Storage","Use Master Storage","Check Other Information","Cancel"; break;
				// Save, Storage, and Pushcart only (Kafra Warehouse)
				case 7:	setarray @K_Menu0$[0],"Save","Use Storage","Use Master Storage","Rent a Pushcart","Cancel"; break;
				// Save, Storage, Other Check information. (Turbo track)
				case 8: setarray @K_Menu0$[0],"Save","Use Storage","Use Master Storage","Check Other Information","Cancel"; break;
				// No Save (Rune Knight)
				case 9: setarray @K_Menu0$[0],"Use Storage","Rent a Pushcart","Use Teleport Service","Use Master Storage","Check Other Information","Cancel"; break;
				// Storage, Save, and Pushcart (Dewata, reorder of case 7)
				case 10: setarray @K_Menu0$[0],"Use Storage","Use Master Storage","Save","Rent a Pushcart","Cancel"; break;
				// Default message (obsolete)
				default: setarray @K_Menu0$[0],"Save","Use Storage","Use Teleport Service","Use Master Storage","Rent a Pushcart","Check Other Information","Cancel"; break;
			}
		}
		set .@menu$,"";
		for (set .@i, 0; .@i < getarraysize(@K_Menu0$); set .@i, .@i + 1)
			set .@menu$, .@menu$ + @K_Menu0$[.@i] + ":";
		set .@j, select (.@menu$) - 1;
		if (@K_Menu0$[.@j] == "Save") {
			return;
		} else if (@K_Menu0$[.@j] == "Use Storage") {
			// Do not charge for Guild Storage
			if(getarg(0) == 2)
				callfunc "F_KafStor",2,0,0;
			else
				callfunc "F_KafStor",0,getarg(3),getarg(0);
			next;
		} else if (@K_Menu0$[.@j] == "Use Teleport Service") {
			// Display Einbroch "No Teleport Service" notice.
			//if(getarg(1) != 4) 
			callfunc "F_KafTele",0;
			// else {
				// mes "[Kafra Employee]";
				// mes "Because of the ^FF0000Limited";
				// mes "Transport Agreement^000000, the";
				// mes "Kafra Corporation cannot";
				// mes "provide Teleport Services";
				// mes "in the Schwaltzvalt Republic.";
				// next;
				// mes "[Kafra Employee]";
				// mes "We ask that you please";
				// mes "use the Airship Service";
				// mes "instead. Thank you for your";
				// mes "understanding and cooperation.";
			// }
			next;
		} else if (@K_Menu0$[.@j] == "Rent a Pushcart") {
			if(callfunc("F_KafCart",getarg(0),getarg(4)) == 1)
				next;
		} else if (@K_Menu0$[.@j] == "Check Other Information") {
			callfunc "F_KafInfo",getarg(2);
			next;
		} else if (@K_Menu0$[.@j] == "Cancel"){
			callfunc "F_KafEnd",getarg(0),0;
			end;
		} else if (@K_Menu0$[.@j] == "Use Guild Storage") {
			callfunc "F_KafStor",1,0;
			next;
		} else if (@K_Menu0$[.@j] == "Use Master Storage") {
			callfunc "F_KafStor",3,200;
			next;
		}
	}
}

//===================================================
// Storage Function
//===================================================
function	script	F_KafStor	{
	// Unable to access Guild Storage (Busy)
	if(getarg(0) == 1){
		if(guildopenstorage()){
			mes "[Kafra Employee]";
			mes "I'm sorry but another guild member is using the guild storage";
			mes "right now.  Please wait until that person is finished.";
			close2;
			cutin "", 255;
			end;
		}
		cutin "", 255;
		close;
	}
	
	//master storage
	if(getarg(0) == 3){
		if(Zeny<getarg(1)){
			mes "[Kafra Employee]";
			// Standard Message
			mes "I'm sorry, but you don't";
			mes "have enough zeny to use";
			mes "the Storage Service. Our";
			mes "Storage access fee is "+getarg(1)+" zeny.";
			return;
		}
		Zeny -= getarg(1);
		set RESRVPTS, RESRVPTS + (getarg(1)/5);
		if(masteropenstorage()){
			mes "[Kafra Employee]";
			mes "I'm sorry but another character member is using the master storage";
			mes "right now. Only one character can access the master storage at a time.";
			close2;
			cutin "", 255;
			end;
		}
		cutin "", 255;
		close;
	}
	
	// Unable to access Normal Storage (Insufficient Basic Skills)
	if(basicskillcheck() && getskilllv("NV_BASIC") < 6){
		mes "[Kafra Employee]";
		// Niflheim Specific Message
		if (getarg(2) == 1) {
			mes "^666666S-s-ssoooorry,";
			mes "y-you're a-a-aaaa";
			mes "Nooviiice... N-neeeds";
			mes "B-basic sssskill l-level 6...^000000";
			return;
		}
		mes "I'm sorry, but you";
		mes "need the Novice's";
		mes "Basic Skill Level 6 to";
		mes "use the Storage Service.";
		return;
	}
	// Accessing Normal Storage (Skipped if accessing Storage from Guild castle)
	if(getarg(0) != 2){
		// Consume "Free Ticket for Kafra Storage" if available.
		if(countitem(7059)) delitem 7059,1;
		else {
			if(Zeny<getarg(1)){
				mes "[Kafra Employee]";
				// Niflheim Specific Message
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^666666Zeeeeeny...";
					mes "M-more z-zeny...!";
					mes "N-neeed 150... zeny...";
					mes "Ergh! T-taking bl-blood~!^000000";
					return;
				}
				// Standard Message
				mes "I'm sorry, but you don't";
				mes "have enough zeny to use";
				mes "the Storage Service. Our";
				mes "Storage access fee is "+getarg(1)+" zeny.";
				return;
			}
			Zeny -= getarg(1);
			set RESRVPTS, RESRVPTS + (getarg(1)/5);
		}
	}

	// Surpress the "End" message if in guild castle.
	if (getarg(0) != 2) {
		// Niflheim specific Message
		if (getarg(2) == 1) {
			percentheal 0,-10;
			mes "[Kafra Employee]";
			for (set .@i, 0; .@i < 5; set .@i, .@i + 1)
				mes "^666666Thank you.. for... using...^000000";
		}
		// Normal message
		else {
			mes "[Kafra Employee]";
			mes "Here, let me open";
			mes "your Storage for you.";
			mes "Thank you for using";
			mes "the Kafra Service.";
		}
	}
	callfunc("F_CheckKafCode");	//check your storage password, if set
	close2;
	openstorage;
	cutin "", 255;
	end;
}


//===================================================
// Teleport Function
//===================================================
function	script	Go	{
	if(countitem(7060) && BaseLevel < 80) delitem 7060,1;
	else {
		if (Zeny < @KafraWarpingCost){ //TODO DUNGEONS
			mes "[Kafra Employee]";
			mes "I'm sorry, but you don't have";
			mes "enough zeny for the Teleport";
			mes "Service. The fee to teleport";
			mes "is "+@KafraWarpingCost+"z.";
			if(countitem(7060)) {
				mes "You are above level 80. There you are too powerful to use newbie tickets.";
			}
			close2;
			cutin "", 255;
			end;
		}
		Zeny -= @KafraWarpingCost;
		set RESRVPTS, RESRVPTS + (@KafraWarpingCost/16);
	}
	
	
	warp getarg(0),getarg(1,0),getarg(2,0);
	getmapxy(lastwarp$,lastwarpx,lastwarpy,0);
	end;
}

function	script	F_KafTele	{
	if(BaseLevel <= 60) {
		set .@LastWarp,100;
	} else {
		set .@LastWarp, (BaseLevel - 59) * 50;
	}
	set @KafraWarpingTowns,(.@LastWarp*15)/10;
	set @KafraWarpingDungeons,.@LastWarp*5;
	if(getarg(0) == 0) {
		mes "[Kafra Employee]";
		mes "Please choose";
		mes "your destination.";
		if(countitem(7060) && BaseLevel >= 80) {
			mes "You are too powerful to use newbie tickets.";
		}
	}
	menu	"Last Warp ^777777["+lastwarp$+"]^000000 ( "+.@LastWarp+"z )", -,
			"Town ( "+@KafraWarpingTowns+"z )", Towns, 
			"Dungeon ( "+@KafraWarpingDungeons+"z )", Dungeons;

		
	if (lastwarp$ == "") {
		mes "[Kafra Employee]";
		mes "I'm terribly sorry but";
		mes "you haven't warped";
		mes "anywhere yet.";
	} else {
		@KafraWarpingCost = .@LastWarp;
		Go(lastwarp$,lastwarpx,lastwarpy);
	}
	close2;
	cutin "",255;
	end;

// --------------------------------------------------
//	Town Menu:
// --------------------------------------------------
Towns:
@KafraWarpingCost = @KafraWarpingTowns;
menu	"Prontera",T1, "Alberta",T2, "Aldebaran",T3, "Amatsu",T4, "Ayothaya",T5,
		"Comodo",T6, "Comodo Pharos Beacon",T29,"Einbech",T7,	"Einbroch",T8, "Geffen",T9, "Gonryun",T10, 
		"Hugel",T11, "Izlude",T12, "Lighthalzen",T13, "Louyang",T14, "Lutie",T15,
		"Manuk",T16, "Midgarts Expedition Camp",T17, "Morroc",T18, "Moscovia",T19, 
		//"Nameless Island",T20, 
		"Niflheim",T21, "Payon",T22, "Rachel",T23, "Splendide",T24, 
		//"Thor Camp",T25, 
		"Umbala",T26, "Veins",T27, "Yuno",T28;

T1: Go("prontera",140,91);
T2: Go("alberta",28,234);
T3: Go("aldebaran",140,131);
T4: Go("amatsu",111,148);
T5: Go("ayothaya",208,166);
T6: Go("comodo",209,143);
T7: Go("einbech",63,35);
T8: Go("einbroch",234,206);
T9: Go("geffen",119,59);
T10: Go("gonryun",160,120);
T11: Go("hugel",96,145);
T12: Go("izlude",128,114);
T13: Go("lighthalzen",158,92);
T14: Go("louyang",217,100);
T15: Go("xmas",147,134);
T16: Go("manuk",282,138);
T17: Go("mid_camp",210,288);
T18: Go("morocc",156,93);
T19: Go("moscovia",223,184);
//T20: Go("nameless_n",256,215);
T21: Go("niflheim",202,174);
T22: Go("payon",160,58);
T23: Go("rachel",130,110);
T24: Go("splendide",201,147);
//T25: Go("thor_camp",246,68);
T26: Go("umbala",97,153);
T27: Go("veins",216,123);
T28: Go("yuno",157,51);		
T29: Go("cmd_fild07",132,127);
// --------------------------------------------------
//	Dungeon Menu:
// --------------------------------------------------
Dungeons:
@KafraWarpingCost = @KafraWarpingDungeons;
menu	"Abyss Lakes",D1, "Amatsu Dungeon",D2, "Anthell",D3,
		"Ayothaya Dungeon",D4, 
		// "Beach Dungeon",D5,
		 "Bio Labs",D6,
		"Byalan Dungeon",D8,
		"Coal Mines",D10, "Culvert",D11, "Cursed Abbey",D12, 
		//"Dewata Dungeon",D13,
		"Einbroch Dungeon",D14, 
		//"Gefenia",D15,
		//"Geffen Dungeon",D16,
		"Glast Heim Castle",D50, "Glast Heim Chivalry",D51, "Glast Heim Church",D52, "Glast Heim Staircase",D53, 
		//"Gonryun Dungeon",D18, 
		"Hidden Dungeon",D19,
		"Ice Dungeon",D20, "Juperos",D21, "Kiel Dungeon",D22, 
		//"Louyang Dungeon",D23,
		"Magma Dungeon",D24, 
		//"Malangdo Dungeon",D25, 
		"Moscovia Dungeon",D26,
		//"Odin Temple",D28, 
		"Orc Dungeon",D29,
		"Payon Dungeon",D30, "Pyramids",D31, "Rachel Sanctuary",D32,
		// "Scaraba Hole",D33,
		"Sphinx",D34, "Sunken Ship",D35, "Thanatos Tower",D36,
		"Thor Volcano",D37, "Toy Factory",D38, 
		"Turtle Dungeon",D39, "Umbala Dungeon",D40;
		next;

D1: Go("hu_fild05",177,211);
D2: Go("ama_in02",120,180);
D3: Go("cmd_fild08",330,355);
D4: Go("ayo_fild02",279,150);
// beach dun direkt in comodo
D6: Go("lighthalzen",308,302);
D8: Go("izlu2dun",108,88);
//D9: Go("c_tower1",199,159); direkt in alde
D10: Go("mjolnir_02",79,363);
D11: Go("prt_fild05",274,208);
D12: Go("nameless_n",255,215);
D14: Go("einbech",138,249);
//D15: Go("gefenia01",40,103); direkt in geffen
D16: Go("gef_tower",153,31);
//D18: Go("gon_dun01",153,53); direkt in gon
D19: Go("prt_fild01",136,369);
D20: Go("ra_fild01",233,327);
D21: Go("yuno_fild07",212,175);
D22: Go("yuno_fild08",74,174);
//D23: Go("lou_dun01",218,196); direkt in louyang
D24: Go("yuno_fild03",34,139);
D26: Go("moscovia",140,54);
//D28: Go("odin_tem01",298,167); schiff
D29: Go("gef_fild10",65,335);
D30: Go("pay_arche",39,131);
D31: Go("moc_ruins",60,161);
D32: Go("ra_temple",119,153);
D34: Go("moc_fild19",108,99);
D35: Go("alb2trea",85,107);
D36: Go("hu_fild01",140,158);
D37: Go("ve_fild03",168,235);
D38: Go("xmas",143,311);
D39: Go("tur_dun01",154,49);
D40: Go("um_dun01",42,31);
D50: Go("glast_01",200,294);
D51: Go("glast_01",74,193);
D52: Go("glast_01",200,134);
D53: Go("glast_01",54,108);
}
//===================================================
// Cart Function
//===================================================
function	script	F_KafCart	{
	// Ensure that the class wanting to rent a pushcart is a merchant
	if(BaseClass != Job_Merchant){
		mes "[Kafra Employee]";
		mes "I'm sorry, but the";
		mes "Pushcart rental service";
		mes "is only available to Merchants,";
		mes "Blacksmiths, Master Smiths,";
		mes "Alchemists and Biochemists.";
		return 1;
	}
	// Make sure the invoking character does not have a cart already
	else if(checkcart() == 1){
		mes "[Kafra Employee]";
		mes "You already have";
		mes "a Pushcart equipped.";
		mes "Unfortunately, we can't";
		mes "rent more than one to";
		mes "each customer at a time.";
		return 1;
	}
	// Consume "Free Ticket for the Cart Service" if available.
	// Do not consume if in Guild castle.
	if(countitem(7061) > 0 && getarg(0) != 2) delitem 7061,1;
	else {
		mes "[Kafra Employee]";
		mes "The Pushcart rental";
		mes "fee is "+getarg(1)+" zeny. Would";
		mes "you like to rent a Pushcart?";
		next;
		if (select("Rent a Pushcart.:Cancel") == 2) return 0;
			if(Zeny<getarg(1)){
				mes "[Kafra Employee]";
				mes "I'm sorry, but you";
				mes "don't have enough";
				mes "zeny to pay the Pushcart";
				mes "rental fee of "+getarg(1)+" zeny.";
				return 1;
			}
			Zeny -= getarg(1);
			set RESRVPTS, RESRVPTS + 48;
	}
	setcart;
	return 1;
}

//===================================================
// Special Reserve Points Function
//===================================================
function	script	F_KafInfo	{
	// Uncomment next line to block Kafra Storage Protection
	//.@block = 1;
	setarray .@m$, "Check Special Reserve Points.","Storage Password Service","Kafra Employee Locations","Cancel";
	if (getarg(0) == 2) deletearray .@m$[1],2; // Port Malaya, Rune Knight: remove Storage Password and Kafra Employee Locations
	else if (.@block) deletearray .@m$[1],1; // Remove Storage Password
	while (true) {
		.@j = select(implode(.@m$,":")) - 1;
		if (.@m$[.@j] == "Check Special Reserve Points.") {
			mes "[Kafra Employee]";
			mes "Let's see...";
			mes strcharinfo(0) + "...";
			mes "Ah, you have a total of";
			mes RESRVPTS+ " Special Reserve Points.";
			next;
			mes "[Kafra Employee]";
			mes "You can exchange your";
			mes "Special Reserve Points for";
			mes "rewards at the Kafra Main Office in Al De Baran. Please use our";
			mes "convenient services to see the benefits of our rewards program.";
			next;
			if(getarg(0) == 1) return;
		} else if (.@m$[.@j] == "Kafra Employee Locations") {
			for (set .@i, 0; .@i <= 3; set .@i, .@i + 1)
				viewpoint 1,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			next;
			for (set .@i, 0; .@i <= 3; set .@i, .@i + 1)
				viewpoint 2,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
		} else if (.@m$[.@j] == "Storage Password Service") {
			callfunc("F_SetKafCode","[Kafra Employee]","Kafra Services");
		} else {
			return;
		}
	}
}

// === End Function ==================================================
//  arg(0): used to determine what message to display.
//  arg(1): used to determine if save message is displayed.
//  arg(2): used to display the name of the area you're saving in.
//===================================================================
function	script	F_KafEnd	{
	mes "[Kafra Employee]";
	if(getarg(1)==1) {
		// Save
		mes "Your Respawn Point";
		mes "has been saved here";
		mes getarg(2)+".";
		mes "Thank you for using";
		mes "the Kafra Services.";
	} else if(getarg(0)==0 || getarg(0)==5) {
		// Generic End
		mes "We, here at Kafra Corporation,";
		mes "are always endeavoring to provide you with the best services. We hope that we meet your adventuring needs and standards of excellence.";
	} else if(getarg(0)==1) {
		// Niflheim End
		percentheal 0,- 25;
		mes "^666666Kaffffra n-never";
		mes "diiiiiiiiiiiiiies. On...";
		mes "On y-yooour siiiiide~^000000";
	}
	close2;
	cutin "", 255;
	end;
}

//===================================================
// Check Storage Password Function
//===================================================
function	script	F_CheckKafCode	{
	if(!#kafra_code) return;
	mes "Enter your storage password:";
	set @code_,0;
	input @code_;
	if(@code_ != #kafra_code) {
		dispbottom "Wrong storage password.";
		close2;
		cutin "",255;
		end;
	}
	set @kafcode_try,0;
	set @code_,0;
	return;
}

// === Set / Change / Clear Storage Password Function ===
// getarg(0): NPC Name
// getarg(1): Company Name
//=======================================================
function	script	F_SetKafCode	{
	mes getarg(0);
	if(#kafra_code) {
		mes "Your storage is protected with a password. What would you do now?";
		next;
		switch (select("Change old password -> 5000z:Remove storage password -> 1000z:Cancel")) {
			case 1:
				mes getarg(0);
				mes "At first, please enter your ^0000FFold password^000000.";
				set @code,callfunc("F_EntKafCode");
				if(!@code || @code != #kafra_code) {
					mes "Wrong password. You can't set a new password.";
					emotion e_hmm;
					break;
				}
				next;
				callsub S_SET,getarg(0),getarg(1);
				break;
			case 2:
				mes getarg(0);
				mes "Please, enter your password before its removal.";
				set @code,callfunc("F_EntKafCode");
				if(!@code) {
					mes "The password hasn't been removed.";
					emotion e_hmm;
					break;
				}
				next;
				mes getarg(0);
				if(Zeny < 1000) {
					mes "You don't have enough zeny.";
					emotion e_cash;
					break;
				}
				Zeny -= 1000;
				if(@code == #kafra_code) {
					set #kafra_code,0;
					mes "You've successfully cleared your storage password.";
					mes "Thank you for using "+getarg(1)+".";
					emotion e_thx;
				} else {
					mes "Wrong password. We won't return your 1000z.";
					mes "Please, next time enter correct password.";
					emotion e_sry;
				}
				break;
			case 3:
				break;
		}
	} else {
		mes getarg(1)+" proudly presents you a new service:";
		mes "Additional storage protection with a password.";
		next;
		switch (select("Set new password -> 5000z:Cancel")) {
			case 1: callsub S_SET,getarg(0),getarg(1); break;
			case 2: break;
		}
	}
	close2;
	cutin "",255;
	end;

S_SET:
	mes getarg(0);
	mes "Now enter your ^FF0000new password^000000 to protect your storage from thieves.";
	set @code,callfunc("F_EntKafCode");
	if(!@code) {
		mes "The password hasn't been changed.";
		emotion e_hmm;
		return;
	}
	next;
	mes getarg(0);
	if(Zeny < 5000) {
		mes "You don't have enough zeny.";
		emotion e_cash;
		return;
	}
	Zeny -= 5000;
	set #kafra_code,@code;
	mes "You've protected your storage with a secret password.";
	mes "Thank you for using "+getarg(1)+".";
	emotion e_thx;
	return;
}

//===================================================
// Basic Password Validation Function
//===================================================
function	script	F_EntKafCode	{
	mes "Enter a number 1000~10000000:";
	set @code_,0;
	set @kafcode_try,@kafcode_try+1;
	if(@kafcode_try>10) {
		set @kafcode_try,0;
		logmes "Hack: Tried to fit storage password.";
	}
	if(input(@code_) == 1) {
		mes "You can't use such big password.";
		return 0;
	}
	if(@code_<1000) {
		mes "You shouldn't use such short password.";
		return 0;
	}
	return @code_;
}

//===================================================
// Warp Points / View Points Function
//===================================================
 function	script	F_KafSet	{
	// cleararray @wrpC$[0],"",getarraysize(@wrpC$);
	// cleararray @wrpD$[0],"",getarraysize(@wrpD$);
	// cleararray @wrpP$[0],"",getarraysize(@wrpP$);
	// cleararray @viewpX[0],"",getarraysize(@viewpX);
	// cleararray @viewpY[0],"",getarraysize(@viewpY);
	// if (strnpcinfo(4) == "prontera") {
		// setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morroc", "Orc Dungeon", "Alberta";
		// setarray @wrpP[0], 600, 1200, 1200, 1200, 1200, 1800;
		// setarray @viewpX[0], 151, 29, 282, 152;
		// setarray @viewpY[0], 29, 207, 200, 326;
	// } else if (strnpcinfo(4) == "alberta") {
		// setarray @wrpP[0], 1200, 1800, 1800;
		// setarray @wrpD$[0], "Payon", "Morroc", "Prontera";
		// setarray @viewpX[0], 28, 113, 0, 0;
		// setarray @viewpY[0], 229, 60, 0, 0;
	// } else if (strnpcinfo(4) == "aldebaran") {
		// setarray @wrpP[0], 1200, 1200, 1800, 1700;
		// setarray @wrpD$[0], "Geffen", "Juno" , "Izlude", "Mjolnir Dead Pit";
	// } else if (strnpcinfo(4) == "comodo" ) {
		// setarray @wrpD$[0], "Morroc", "Comodo Pharos Beacon", "Umbala";
		// setarray @wrpP[0], 1800, 1200, 1800;
	// } else if (strnpcinfo(4) == "cmd_fild07") {
		// setarray @wrpD$[0], "Comodo", "Morroc";
		// setarray @wrpP[0], 1200, 1200;
	// } else if (strnpcinfo(4) == "geffen") {
		// setarray @wrpD$[0], "Prontera", "Al De Baran", "Orc Dungeon", "Mjolnir Dead Pit";
		// setarray @wrpP[0], 1200, 1200, 1200, 1700;
		// setarray @viewpX[0], 120, 203;
		// setarray @viewpY[0], 62, 123;
	// } else if (strnpcinfo(4) == "izlude") {
		// setarray @wrpD$[0], "Geffen", "Payon", "Morroc", "Al De Baran";
		// setarray @wrpP[0], 1200, 1200, 1200, 1800;
	// } else if (strnpcinfo(4) == "morocc") {
		// setarray @wrpD$[0], "Prontera", "Payon", "Alberta", "Comodo", "Comodo Pharos Beacon";
		// setarray @wrpP[0], 1200, 1200, 1800, 1800, 1200;
		// setarray @viewpX[0], 156, 163, 28, 292;
		// setarray @viewpY[0], 97, 260, 167, 211;
	// } else if (strnpcinfo(4) == "umbala") {
		// setarray @wrpD$[0], "Comodo";
		// setarray @wrpP[0], 1800;
	// } else if (strnpcinfo(4) == "payon") {
		// setarray @wrpD$[0], "Prontera", "Alberta", "Morroc";
		// setarray @wrpP[0], 1200, 1200, 1200;
	// } else if (strnpcinfo(4) == "yuno") {
		// setarray @wrpD$[0], "Al De Baran";
		// setarray @wrpP[0], 1200;
		// setarray @viewpX[0], 328, 278, 153, 0;
		// setarray @viewpY[0], 108, 221, 187, 0;
	// } else if (strnpcinfo(4) == "job3_rune01") {
		// setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morroc", "Alberta";
		// setarray @wrpP[0], 600, 1200, 1200, 1200, 1800;
	// }
	// for (set .@i, 0; .@i < getarraysize(@wrpD$); set .@i, .@i + 1)
		// setd "@wrpC$["+.@i+"]",getd("@wrpD$["+.@i+"]")+" -> "+getd("@wrpP["+.@i+"]");
	// setd "@wrpC$["+getarraysize(@wrpC$)+"]","Cancel";
	 return;
 }
