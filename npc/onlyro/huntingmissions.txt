//===== rAthena Script =======================================
//= Hunting Missions
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.3a
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: =========================================
//= Random hunting missions.
//= Rewards are based on quest difficulty.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.1 Small improvements and fixes.
//= 1.2 Added party support and replaced blacklists with an
//=     SQL query, both thanks to AnnieRuru.
//= 1.3 Re-added a blacklist adapted for the SQL query.
//= 1.3a Added mission reset options.
//============================================================

prontera,146,76,6	script	Hunting Missions	951,{
function Chk; function Cm;
	mes "[Hunting Missions]";
	mes "Hello, "+strcharinfo(0)+"!";
	if (!Mission_Delay) {
		next;
		mes "[Hunting Missions]";
		mes "I can't find any records...";
		mes "You must be new here!";
		emotion e_omg;
		next;
		callsub Mission_Info;
		emotion e_go;
		set Mission_Delay,1;
		close;
	}
	mes rand(2)?"Working hard, as always...":"Not slacking, I hope...";
	if (Mission_Delay > gettimetick(2) && .Delay) {
		mes "I'm afraid you'll have to wait "+callfunc("Time2Str",Mission_Delay)+" before taking another mission.";
		//close;
	} else {
		mes "Is there anything I can help";
		mes "you with?";
		mes "Your next mission perhaps?";
	}
	mes "^777777~ You've completed ^0055FF"+Mission_Total+"^777777 mission"+((Mission_Total == 1)?"":"s")+". ~^000000";
	next;
	switch(select(((!Mission0)?" ~ Personal ^007700easy^000000 Mission (^777777First Class / 3h^000000): ~ Personal Mission (^777777Second Class / 9h^000000): ~ Personal ^990000insane^000000 Mission (^777777Rebirth / 18h^000000): ~ Public ^990000hard^000000 Mission (^777777Party / 18h^000000)::":":::: ~ Mission Status: ~ Abandon Mission")+": ~ Information: ~ Mission Shop: ~ View Top Hunters: ~ ^777777Cancel^000000")) {
		case 1: // easy
		mes "[Hunting Missions]";
		if (Mission_Count) {
			mes "You've started a mission";
			mes "on another character.";
			close;
		}
		if (Mission_Delay > gettimetick(2) && .Delay) {
			mes "I'm afraid you'll have to wait "+callfunc("Time2Str",Mission_Delay)+" before taking another mission.";
			close;
		}

		mes "You are about to accept a mission.";
		mes "You can't accept another mission for 3 hours.";
		mes "Are you sure you want to do this?";
		next;
		switch(select("Let's go on a hunt!:I think I'll pass on this one.")) {
			case 1:
					mes "[Hunting Missions]";
		set Mission_Quests, rand(.Quests[0], .Quests[1]);
		mes "You must hunt:";
		query_sql("SELECT ID FROM `mob_db` WHERE left(Sprite, 4) != 'meta' AND left(Sprite, 2) != 'E_' AND ~Mode & 32 AND EXP > 0 AND HP <= 3000 AND LV <= 45 AND MVP1id = 0 AND DropCardid > 4000 AND DropCardid < 5000 AND ID < 2000 AND instr('"+.Blacklist$+"',ID) = 0 ORDER BY rand() LIMIT "+Mission_Quests, .@mob);
		for (set .@i,0; .@i<Mission_Quests; set .@i,.@i+1) {
			setd "Mission"+.@i, .@mob[.@i];
			setd "Mission"+.@i +"_",0;
		}
		set Mission_Count, rand(.Count[0]*(.Quests[1]+1-Mission_Quests),.Count[1]*(.Quests[1]+2-Mission_Quests));
		if (.Delay) set Mission_Delay, gettimetick(2)+(3*3600);
		callsub Mission_Status;
		next;
		mes "[Hunting Missions]";
		mes "Report back when";
		mes "you've finished.";
		mes "Good luck!";
		close;
						case 2:
			mes "[Hunting Missions]";
			mes "I'll see you around then.";
			close;
		}
	case 2: // medium
		mes "[Hunting Missions]";
		if (Mission_Count) {
			mes "You've started a mission";
			mes "on another character.";
			close;
		}
		if (Mission_Delay > gettimetick(2) && .Delay) {
			mes "I'm afraid you'll have to wait "+callfunc("Time2Str",Mission_Delay)+" before taking another mission.";
			close;
		}
		mes "You are about to accept a mission.";
		mes "You can't accept another mission for 9 hours.";
		mes "Are you sure you want to do this?";
		next;
		switch(select("Let's go on a hunt!:I think I'll pass on this one.")) {
			case 1:
					mes "[Hunting Missions]";
		set Mission_Quests, rand(.Quests[0], .Quests[1]);
		mes "You must hunt:";
		query_sql("SELECT ID FROM `mob_db` WHERE left(Sprite, 4) != 'meta' AND left(Sprite, 2) != 'E_' AND ~Mode & 32 AND EXP > 250 AND LV <= 75 AND MVP1id = 0 AND DropCardid > 4000 AND DropCardid < 5000 AND ID < 2000 AND instr('"+.Blacklist$+"',ID) = 0 ORDER BY rand() LIMIT "+Mission_Quests, .@mob);
		for (set .@i,0; .@i<Mission_Quests; set .@i,.@i+1) {
			setd "Mission"+.@i, .@mob[.@i];
			setd "Mission"+.@i +"_",0;
		}
		set Mission_Count, rand(.Count[0]*(.Quests[1]+1-Mission_Quests),.Count[1]*(.Quests[1]+2-Mission_Quests));
		if (.Delay) set Mission_Delay, gettimetick(2)+(9*3600);
		callsub Mission_Status;
		next;
		mes "[Hunting Missions]";
		mes "Report back when";
		mes "you've finished.";
		mes "Good luck!";
		close;
						case 2:
			mes "[Hunting Missions]";
			mes "I'll see you around then.";
			close;
		}
	case 3:
		mes "[Hunting Missions]";
		if (Mission_Count) {
			mes "You've started a mission";
			mes "on another character.";
			close;
		}
		if (Mission_Delay > gettimetick(2) && .Delay) {
			mes "I'm afraid you'll have to wait "+callfunc("Time2Str",Mission_Delay)+" before taking another mission.";
			close;
		}

		mes "You are about to accept a mission.";
		mes "You can't accept another mission for 18 hours.";
		mes "Are you sure you want to do this?";
		next;
		switch(select("Let's go on a hunt!:I think I'll pass on this one.")) {
			case 1:
			mes "[Hunting Missions]";
			set Mission_Quests, rand(.Quests[0], .Quests[1]);
			mes "You must hunt:";
			query_sql("SELECT ID FROM `mob_db` WHERE left(Sprite, 4) != 'meta' AND left(Sprite, 2) != 'E_' AND ~Mode & 32 AND EXP > 500 AND LV > 30 AND MVP1id = 0 AND DropCardid > 4000 AND DropCardid < 5000 AND ID < 2000 AND instr('"+.Blacklist$+"',ID) = 0 ORDER BY rand() LIMIT "+Mission_Quests, .@mob);
			for (set .@i,0; .@i<Mission_Quests; set .@i,.@i+1) {
				setd "Mission"+.@i, .@mob[.@i];
				setd "Mission"+.@i +"_",0;
			}
			set Mission_Count, rand(.Count[0]*((.Quests[1])+1-Mission_Quests)*12,.Count[1]*((.Quests[1])+2-Mission_Quests)*15);
			if (.Delay) set Mission_Delay, gettimetick(2)+(18*3600);
			callsub Mission_Status;
			next;
			mes "[Hunting Missions]";
			mes "Report back when";
			mes "you've finished.";
			mes "Good luck!";
			close;
				case 2:
			mes "[Hunting Missions]";
			mes "I'll see you around then.";
			close;
		}
	case 4:
		mes "[Hunting Missions]";
		if (Mission_Count) {
			mes "You've started a mission";
			mes "on another character.";
			close;
		}
		if (Mission_Delay > gettimetick(2) && .Delay) {
			mes "I'm afraid you'll have to wait "+callfunc("Time2Str",Mission_Delay)+" before taking another mission.";
			close;
		}
		mes "Current public mission:";
		if (.GroupMission_Delay <= gettimetick(2)) {
			set .GroupMission_Quests, rand(.Quests[0], .Quests[1]);
			query_sql("SELECT ID FROM `mob_db` WHERE left(Sprite, 4) != 'meta' AND left(Sprite, 2) != 'E_' AND ~Mode & 32 AND EXP < 800 AND EXP > 0 AND MVP1id = 0 AND DropCardid > 4000 AND DropCardid < 5000 AND ID < 2000 AND instr('"+.Blacklist$+"',ID) = 0 ORDER BY rand() LIMIT 1", .@mob);
			setd ".GroupMission"+0, .@mob[0];
			setd ".GroupMission"+0 +"_",0;
			query_sql("SELECT ID FROM `mob_db` WHERE left(Sprite, 4) != 'meta' AND left(Sprite, 2) != 'E_' AND ~Mode & 32 AND EXP > 0 AND MVP1id = 0 AND DropCardid > 4000 AND DropCardid < 5000 AND ID < 2000 AND instr('"+.Blacklist$+"',ID) = 0 ORDER BY rand() LIMIT "+(.GroupMission_Quests-1), .@mob);
			for (set .@i,0; .@i<.GroupMission_Quests-1; set .@i,.@i+1) {
				setd ".GroupMission"+(.@i+1), .@mob[.@i];
				setd ".GroupMission"+(.@i+1) +"_",0;
			}
			set .GroupMission_Delay, gettimetick(2)+(3*3600);
			set .GroupMission_Count, rand(.Count[0]*((.Quests[1])+1-.GroupMission_Quests)*12,.Count[1]*((.Quests[1])+2-.GroupMission_Quests)*15);
		}
		set @f,0;
		deletearray .@j[0], getarraysize(.@j);
		for(set .@i,0; .@i<.GroupMission_Quests; set .@i,.@i+1) {
			set .@j[.@i], getd(".GroupMission"+.@i);
			set .@j[.GroupMission_Quests], .@j[.GroupMission_Quests]+strmobinfo(3,.@j[.@i]);
			set .@j[.GroupMission_Quests+1], .@j[.GroupMission_Quests+1]+(strmobinfo(6,.@j[.@i])/(getbattleflag("base_exp_rate")/100)*.Modifier[0]);
			set .@j[.GroupMission_Quests+2], .@j[.GroupMission_Quests+2]+(strmobinfo(7,.@j[.@i])/(getbattleflag("job_exp_rate")/100)*.Modifier[1]);
			mes " > "+Chk(getd(".GroupMission"+.@i+"_"),.GroupMission_Count)+strmobinfo(1,.@j[.@i])+" ("+getd(".GroupMission"+.@i+"_")+"/"+.GroupMission_Count+")^000000";
		}

		// Reward formulas:
		set .@Mission_Points, (3+(((.GroupMission_Count*.GroupMission_Quests) / ((.GroupMission_Count / 10) + .Count[0])) * ((2 * .@j[.GroupMission_Quests]) / (.GroupMission_Quests+2)) / 10));
		set .@Base_Exp, (.GroupMission_Count*.@j[.GroupMission_Quests+1]) / .GroupMission_Quests;
		set .@Job_Exp, (.GroupMission_Count*.@j[.GroupMission_Quests+2])/ .GroupMission_Quests;
		set .@Zeny, .GroupMission_Count*.GroupMission_Quests*.@j[.@i]*.Modifier[2] / (.GroupMission_Quests*2);
		
		next;
		mes "[Hunting Missions]";
		mes "Current public mission rewards:";
		mes " > Mission Points: ^0055FF"+.@Mission_Points+"^000000";
		mes " > Base Experience: ^0055FF"+Cm(.@Base_Exp)+"^000000";
		mes " > Job Experience: ^0055FF"+Cm(.@Job_Exp)+"^000000";
		mes " > Zeny: ^0055FF"+Cm(.@Zeny)+"^000000";
		next;
		mes "[Hunting Missions]";
		
		mes "This mission is available for another "+callfunc("Time2Str",.GroupMission_Delay)+" before changing to another mission.";
		mes "Do you want to try your luck with this mission?";
		next;
		switch(select("Let's go on a hunt!:I think I'll pass on this one.")) {
			case 1:
				if (.Delay) set Mission_Delay, gettimetick(2)+(18*3600);
				set Mission_Quests, .GroupMission_Quests;
				for (set .@i,0; .@i<.GroupMission_Quests; set .@i,.@i+1) {
					setd("Mission"+.@i, getd(".GroupMission"+.@i));
					setd("Mission"+.@i +"_",0);
				}
				set Mission_Count, .GroupMission_Count;
				mes "[Hunting Missions]";
				mes "Report back when";
				mes "you've finished.";
				mes "Good luck!";
				close;
			case 2:
				mes "[Hunting Missions]";
				mes "I'll see you around then.";
				close;
		}
	case 5:
		mes "[Hunting Missions]";
		mes "Mission status:";
		callsub Mission_Status;
		close;
	case 6:
		mes "[Hunting Missions]";
		mes "Do you really want to";
		mes "abandon your mission?";
		if (.Reset < 0 && .Delay)
			mes "Your delay time will not be reset.";
		else if (.Reset > 0)
			mes "It will cost "+Cm(.Reset)+" Zeny.";
		next;
		switch(select(" ~ Abandon...: ~ ^777777Cancel^000000")) {
		case 1:
			if (.Reset > 0) {
				if (Zeny < .Reset) {
					mes "[Hunting Missions]";
					mes "You don't have enough";
					mes "Zeny to drop this mission.";
					emotion e_sry;
					close;
				}
				set Zeny, Zeny-.Reset;
				emotion e_cash;
			}
			mes "[Hunting Missions]";
			mes "Alright, I've dropped";
			mes "your current mission.";
			specialeffect2 EF_STORMKICK4;
			for(set .@i,0; .@i<Mission_Quests; set .@i,.@i+1) {
				setd "Mission"+.@i,0;
				setd "Mission"+.@i+"_",0;
			}
			set Mission_Count,0;
			close;
		case 2:
			mes "[Hunting Missions]";
			mes "I knew you were kidding!";
			mes "Keep up the good work.";
			emotion e_heh;
			close;
		}
	case 7:
		callsub Mission_Info;
		close;
	case 8:
		mes "[Hunting Missions]";
		mes "You have ^0055FF"+#Mission_Points+"^000000 Mission Points.";
		mes "Use them well!";
		callshop "mission_shop",1;
		npcshopattach "mission_shop";
		end;
	case 9:
		mes "[Hunting Missions]";
		mes "The top hunters are:";
		query_sql("SELECT char_id AS id, (SELECT `name` FROM `char` WHERE char_id = id),`value` FROM `char_reg_num_db` WHERE `key` = 'Mission_Total' ORDER BY CAST(`value` AS SIGNED) DESC LIMIT 5",.@id,.@name$,.@val);
		for(set .@i,0; .@i<5; set .@i,.@i+1)
			mes "  [Rank "+(.@i+1)+"]  "+((.@name$[.@i] == "")?"^777777none":"^0055FF"+.@name$[.@i]+"^000000 : ^FF0000"+.@val[.@i]+" pt.")+"^000000";
		close;
	case 10:
		mes "[Hunting Missions]";
		mes "Nothing? Okay...";
		emotion e_hmm;
		close;
	}

Mission_Status:
	set @f,0;
	deletearray .@j[0], getarraysize(.@j);
	for(set .@i,0; .@i<Mission_Quests; set .@i,.@i+1) {
		set .@j[.@i], getd("Mission"+.@i);
		set .@j[Mission_Quests], .@j[Mission_Quests]+strmobinfo(3,.@j[.@i]);
		set .@j[Mission_Quests+1], .@j[Mission_Quests+1]+(strmobinfo(6,.@j[.@i])/(getbattleflag("base_exp_rate")/100)*.Modifier[0]);
		set .@j[Mission_Quests+2], .@j[Mission_Quests+2]+(strmobinfo(7,.@j[.@i])/(getbattleflag("job_exp_rate")/100)*.Modifier[1]);
		mes " > "+Chk(getd("Mission"+.@i+"_"),Mission_Count)+strmobinfo(1,.@j[.@i])+" ("+getd("Mission"+.@i+"_")+"/"+Mission_Count+")^000000";
	}

	// Reward formulas:
	set .@Mission_Points, (3+(((Mission_Count*Mission_Quests) / ((Mission_Count / 10) + .Count[0])) * ((2 * .@j[Mission_Quests]) / (Mission_Quests+2)) / 10));
	set .@Base_Exp, (Mission_Count*.@j[Mission_Quests+1]) / Mission_Quests;
	set .@Job_Exp, (Mission_Count*.@j[Mission_Quests+2])/ Mission_Quests;
	set .@Zeny, Mission_Count*Mission_Quests*.@j[.@i]*.Modifier[2] / (Mission_Quests*2);

	next;
	mes "[Hunting Missions]";
	mes "Mission rewards:";
	mes " > Mission Points: ^0055FF"+.@Mission_Points+"^000000";
	mes " > Base Experience: ^0055FF"+Cm(.@Base_Exp)+"^000000";
	mes " > Job Experience: ^0055FF"+Cm(.@Job_Exp)+"^000000";
	mes " > Zeny: ^0055FF"+Cm(.@Zeny)+"^000000";
	if (@f) { set @f,0; return; }
	next;
	mes "[Hunting Missions]";
	mes "Oh, you're done!";
	mes "Good work.";
	mes "Here's your reward.";
	emotion e_no1;
	specialeffect2 EF_BENEDICTIO;
	specialeffect2 EF_TRUESIGHT;
	set #Mission_Points, #Mission_Points+.@Mission_Points;
	set BaseExp, BaseExp+.@Base_Exp;
	set JobExp, JobExp+.@Job_Exp;
	set Zeny, Zeny+.@Zeny;
	for(set .@i,0; .@i<Mission_Quests; set .@i,.@i+1) {
		setd "Mission"+.@i,0;
		setd "Mission"+.@i+"_",0;
	}
	set Mission_Count,0;
	set Mission_Total, Mission_Total+1;
	close;

Mission_Info:
	mes "[Hunting Missions]";
	mes "If you so choose, I can assign";
	mes "you a random hunting quest.";
	mes "Some are easier than others, but";
	mes "the rewards increase with difficulty.";
	next;
	mes "[Hunting Missions]";
	mes "Missions points are shared";
	mes "amongst all your characters.";
	if (.Delay) mes "Delay time is, too.";
	mes "Partykills count towards your mission, but only in share range.";
	next;
	mes "[Hunting Missions]";
	mes "Different mission types have different delays which applies to all types.";
	mes "That's everything~";
	return;

function Chk {
	if (getarg(0) < getarg(1)) { set @f,1; return "^FF0000"; }
	else return "^00FF00"; }

function Cm {
	set .@str$, getarg(0);
	for(set .@i,getstrlen(.@str$)-3; .@i>0; set .@i,.@i-3)
		set .@str$, insertchar(.@str$,",",.@i);
	return .@str$; }

OnBuyItem:
	set @cost,0;
	for(set .@i,0; .@i<getarraysize(@bought_nameid); set .@i,.@i+1)
		for(set .@j,0; .@j<getarraysize(.Shop); set .@j,.@j+2)
			if (@bought_nameid[.@i] == .Shop[.@j]) {
				set @cost, @cost+(.Shop[.@j+1]*@bought_quantity[.@i]);
				break;
			}
	mes "[Hunting Missions]";
	if (@cost > #Mission_Points) mes "You don't have enough Mission Points.";
	else {
		for(set .@i,0; .@i<getarraysize(@bought_nameid); set .@i,.@i+1) {
			getitem @bought_nameid[.@i], @bought_quantity[.@i];
			dispbottom "Purchased "+@bought_quantity[.@i]+"x "+getitemname(@bought_nameid[.@i])+".";
		}
		set #Mission_Points, #Mission_Points-@cost;
		mes "Deal completed.";
		emotion e_cash;
	}
	set @cost,0;
	deletearray @bought_nameid[0], getarraysize(@bought_nameid);
	deletearray @bought_quantity[0], getarraysize(@bought_quantity);
	close;

OnNPCKillEvent:
	if (!getcharid(1) || !.Party) {
		if (!Mission_Count || !Mission0) end;
		for (set .@i, 0; .@i<Mission_Quests; set .@i,.@i+1) {
			if (strmobinfo(1,killedrid) == strmobinfo(1,getd("Mission"+.@i))) {
				if (getd("Mission"+.@i+"_") < Mission_Count) {
					dispbottom "[Hunting Mission] Killed "+(set(getd("Mission"+.@i+"_"),getd("Mission"+.@i+"_")+1))+" of "+Mission_Count+" "+strmobinfo(1,killedrid)+".";
					end;
				}
			}
		}
	} else if (.Party) {
		set .@mob, killedrid;
		//getmapxy(.@map1$,.@x1,.@y1,0);
		getpartymember getcharid(1),1;
		getpartymember getcharid(1),2;
		set .@baselevel,BaseLevel;
		for(set .@i,0; .@i<$@partymembercount; set .@i,.@i+1) {
			if (isloggedin($@partymemberaid[.@i], $@partymembercid[.@i])) {
				attachrid $@partymemberaid[.@i];
				if( ( BaseLevel - .@baselevel ) <= .party_level_range && ( .@baselevel - BaseLevel ) <= .party_level_range ) {	
					if (Mission_Count && Mission0 && Hp > 0) {
						//getmapxy(.@map2$,.@x2,.@y2,0);
						//if ((.@map1$ == .@map2$ || .Party == 1) && (distance(.@x1,.@y1,.@x2,.@y2) <= 30 || .Party < 3)) {
						if(checkidle() < 11) {
							for(set .@j,0; .@j<Mission_Quests; set .@j,.@j+1) {
								if (strmobinfo(1,.@mob) == strmobinfo(1,getd("Mission"+.@j))) {
									if (getd("Mission"+.@j+"_") < Mission_Count) {
										dispbottom "[Hunting Mission] Killed "+(set(getd("Mission"+.@j+"_"),getd("Mission"+.@j+"_")+1))+" of "+Mission_Count+" "+strmobinfo(1,.@mob)+".";
										break;
									}
								}
							}
						}
						//}
					}
				}
			}
		}
	}
	end;

OnInit:
	set .Delay,12;		// Quest delay, in hours (0 to disable).
	setarray .Quests[0],3,5;		// Number of subquests per mission (increases rewards).
	set .Party,3;		// Party options: 0 (exclude party kills), 1 (include party kills), 2 (same map only), 3 (screen area only)
	set .Reset,-1;		// Reset options: -1 (abandoning mission sets delay time), 0 (no delay time), [Zeny] (cost to abandon mission, no delay time)
	setarray .Count[0],	// Min and max monsters per subquest (increases rewards).
		20,30;
	setarray .Modifier[0],	// Multipliers for Base Exp, Job Exp, and Zeny rewards.
		2,2,1;
	setarray .Shop[0],	// Reward items: <ID>,<point cost> (about 10~20 points per hunt).
		12109,20,658,25;
	set .Blacklist$,	// Blacklisted mob IDs.
		"1062,1088,1183,1186,1200,1212,1220,1221,1234,1235,"+
		"1244,1245,1250,1268,1290,1293,1294,1296,1298,1299,"+
		"1300,1301,1303,1304,1305,1306,1308,1309,1311,1313,"+
		"1515,1588,1618,1676,1677,1678,1679,1796,1797,1974,"+
		"1975,1976,1977,1978,1979,1247";
		
	set .party_level_range, 10;
	
	npcshopdelitem "mission_shop",512;
	for(set .@i,0; .@i<getarraysize(.Shop); set .@i,.@i+2)
		npcshopadditem "mission_shop", .Shop[.@i], .Shop[.@i+1];
	end;
}
-	shop	mission_shop	-1,512:-1
