//===== Hercules Script ======================================
//= Battleground Eye of the Storm
-	script	BG_EOS	-1,{
	end;

OnGuillaumeQuit:
OnGuillaumeDie:
	if( getvariableofnpc(.BG_Status,"BG_EOS") == 1 && getvariableofnpc(.Flag_Carrier,"Neutral_Flag") == getcharid(0) )
	{ // Drop Flag
		pcblock 1,0; // UnBlock Skills/Items
		getmapxy .@m$, .@x, .@y, 0;
		movenpc "Neutral_Flag", .@x, .@y;
		mapannounce "bat_a02","Guillaume have dropped the Flag",1,0x00FF00;
		set getvariableofnpc(.Flag_Status,"Neutral_Flag"), 0; // OnFloor
		set getvariableofnpc(.Flag_Carrier,"Neutral_Flag"), 0;
		initnpctimer "Neutral_Flag";
		enablenpc "Neutral_Flag";
	}
	end;

OnCroixQuit:
OnCroixDie:
	if( getvariableofnpc(.BG_Status,"BG_EOS") == 1 && getvariableofnpc(.Flag_Carrier,"Neutral_Flag") == getcharid(0) )
	{ // Drop Flag
		pcblock 1,0; // UnBlock Skills/Items
		getmapxy .@m$, .@x, .@y, 0;
		movenpc "Neutral_Flag", .@x, .@y;
		mapannounce "bat_a02","Croix have dropped the Flag",1,0x00FF00;
		set getvariableofnpc(.Flag_Status,"Neutral_Flag"), 0; // OnFloor
		set getvariableofnpc(.Flag_Carrier,"Neutral_Flag"), 0;
		initnpctimer "Neutral_Flag";
		enablenpc "Neutral_Flag";
	}
	end;

OnBuildTeams:
	set $@BG_EOSt1,bg_create_team("bat_a02",50,374,0,"BG_EOS::OnGuillaumeQuit","BG_EOS::OnGuillaumeDie");
	set $@BG_EOSt2,bg_create_team("bat_a02",42,16,1,"BG_EOS::OnCroixQuit","BG_EOS::OnCroixDie");
	end;

OnReady:
	// BG Variables
	.BG_Status = 1;
	set .Guillaume_Score, 0;
	set .Croix_Score, 0;
	sleep 2000;
	bg_warp $@BG_EOSt1,"bat_a02",353,344;
	bg_warp $@BG_EOSt2,"bat_a02",353,52;
	sleep 2000;
	// Respawn NPC's
	donpcevent "#guieoe_respawn::OnBGStart";
	donpcevent "#croeoe_respawn::OnBGStart";
	// Start Match!
	donpcevent "North_Base::OnBase";
	donpcevent "South_Base::OnBase";
	donpcevent "Neutral_Flag::OnBase";
	donpcevent "::OnStartStorm";
	mapannounce "bat_a02","Welcome to Eye of the Storm. Capture the bases to continuously gain points. The flag is worth bonus points!",1,0x00FF00;
	end;

OnFlash:
	if( getvariableofnpc(.Flag_Carrier,"Neutral_Flag") == getcharid(0) && getvariableofnpc(.BG_Status,"BG_EOS") == 1 )
	{
		getmapxy .@m$, .@x, .@y, 0;
		viewpointmap "bat_a02",1,.@x,.@y,3,0xFFFF00;
		specialeffect2 73;
		emotion e_hlp,1;
		addtimer 2000, "BG_EOS::OnFlash";
		percentheal -5,-5;
	}
	end;

OnGuillaumeFlag:
	viewpointmap "bat_a02",2,0,0,3,0xFFFF00;
	set .@North, getvariableofnpc(.Owner,"North_Base");
	set .@South, getvariableofnpc(.Owner,"South_Base");

	if( .@North == .@South )
	{
		set .Guillaume_Score, .Guillaume_Score + 5;
		mapannounce "bat_a02","Guillaume Team captured the Flag [+5 points]",1,0x0000FF;
	}
	else
	{
		set .Guillaume_Score, .Guillaume_Score + 3;
		mapannounce "bat_a02","Guillaume Team captured the Flag [+3 points]",1,0x0000FF;
	}

	donpcevent "BG_EOS::OnValidateScore";
	donpcevent "Neutral_Flag::OnBase";
	end;

OnCroixFlag:
	viewpointmap "bat_a02",2,0,0,3,0xFFFF00;
	set .@North, getvariableofnpc(.Owner,"North_Base");
	set .@South, getvariableofnpc(.Owner,"South_Base");
	
	if( .@North == .@South )
	{
		set .Croix_Score, .Croix_Score + 5;
		mapannounce "bat_a02","Croix Team captured the Flag [+5 points]",1,0xFF0000;
	}
	else
	{
		set .Croix_Score, .Croix_Score + 3;
		mapannounce "bat_a02","Croix Team captured the Flag [+3 points]",1,0xFF0000;
	}

	donpcevent "BG_EOS::OnValidateScore";
	donpcevent "Neutral_Flag::OnBase";
	end;

OnNorthScore:
	if( set(.@North, getvariableofnpc(.Owner,"North_Base")) == 0 )
		end; // No Owner
	set .@South, getvariableofnpc(.Owner,"South_Base");
	if( .@North == .@South )
	{ // Double Domination
		if( .@North == $@BG_EOSt1 )
			set .Guillaume_Score, .Guillaume_Score + 2;
		else
			set .Croix_Score, .Croix_Score + 2;
	}
	else if( .@North == $@BG_EOSt1 )
		set .Guillaume_Score, .Guillaume_Score + 1;
	else
		set .Croix_Score, .Croix_Score + 1;

	donpcevent "BG_EOS::OnValidateScore";
	end;
	
OnSouthScore:
	if( set(.@South, getvariableofnpc(.Owner,"South_Base")) == 0 )
		end; // No Owner
	set .@North, getvariableofnpc(.Owner,"North_Base");
	if( .@North == .@South )
	{ // Double Domination
		if( .@South == $@BG_EOSt1 )
			set .Guillaume_Score, .Guillaume_Score + 2;
		else
			set .Croix_Score, .Croix_Score + 2;
	}
	else if( .@South == $@BG_EOSt1 )
		set .Guillaume_Score, .Guillaume_Score + 1;
	else
		set .Croix_Score, .Croix_Score + 1;

	donpcevent "BG_EOS::OnValidateScore";
	end;

OnValidateScore:
	if( .Guillaume_Score > 99 )
		set .Guillaume_Score, 99;
	if( .Croix_Score > 99 )
		set .Croix_Score, 99;

	bg_updatescore "bat_a02",.Guillaume_Score,.Croix_Score; // Update Visual Score

	if( .Guillaume_Score < 99 && .Croix_Score < 99 )
		end; // No winners

	donpcevent "Neutral_Flag::OnDisable";
	stopnpctimer "North_Base";
	stopnpctimer "South_Base";
	donpcevent "#guieoe_respawn::OnBGStop";
	donpcevent "#croeoe_respawn::OnBGStop";
	set .BG_Status, 2;
	// =======================================================
	// Team Rewards
	// =======================================================
	if( .Guillaume_Score >= 99 && .Croix_Score >= 99 )
	{ // Tie
		.@t1r = 4;
		.@t2r = 4;
		mapannounce "bat_a02","The battle is over. This is a Tie...!",1,0x00FF00;
	}
	else if( .Guillaume_Score >= 99 )
	{ // Guillaume Won
		.@t1r = 5;
		.@t2r = 3;
		mapannounce "bat_a02","The Guillaume army has won the Battle of Tierra EoS!",1,0x0000FF;
	}
	else if( .Croix_Score >= 99 )
	{ // Croix Won
		.@t1r = 3;
		.@t2r = 5;
		mapannounce "bat_a02","The Croix army has won the Battle of Tierra EoS!",1,0x0000FF;
	}
	.@rqitA = queueiterator($@BG_EOSq1);
	.@rqitB = queueiterator($@BG_EOSq2);

	$@bgFinishedPlayerCount = 0;
	
	while ((set(.currentRID, qinext(.@rqitA))) != 0) {
		attachrid .currentRID;
		$@bgFinishedPlayerCount++;
		setd("$onlyro_bgdelay_"+#master_id, gettimetick(2) + 10);
		getitem 7773,.@t1r;
		getitem 7829,2;
		detachrid;
	}

	while ((set(.currentRID, qinext(.@rqitB))) != 0) {
		attachrid .currentRID;
		$@bgFinishedPlayerCount++;
		setd("$onlyro_bgdelay_"+#master_id, gettimetick(2) + 10);
		getitem 7773,.@t2r;
		getitem 7829,2;
		detachrid;
	}
	
	qiclear(.@rqitA);
	qiclear(.@rqitB);
	queuedel($@BG_EOSq1);
	queuedel($@BG_EOSq2);
	// =======================================================
	set .Guillaume_Score, 0;
	set .Croix_Score, 0;
	sleep 5000;
	// Warps Teams
	bg_warp $@BG_EOSt1,"SavePoint",0,0;
	bg_warp $@BG_EOSt2,"SavePoint",0,0;
	// Destroy Teams
	bg_destroy $@BG_EOSt1;
	set $@BG_EOSt1, 0;
	bg_destroy $@BG_EOSt2;
	set $@BG_EOSt2, 0;
	$@bg_type_ext$ = "BG_EOS";
	donpcevent "bg_queue::OnHasBGFinished";
	bg_updatescore "bat_a02",0,0;
	donpcevent "GEoSNBW::OnDisable";
	donpcevent "GEoSCBW::OnDisable";
	donpcevent "GEoSSBW::OnDisable";
	donpcevent "CEoSNBW::OnDisable";
	donpcevent "CEoSCBW::OnDisable";
	donpcevent "CEoSSBW::OnDisable";
	end;

OnBreak:
	if( getvariableofnpc(.BG_Status,"BG_EOS") != 1 )
		end;

	if( getcharid(4) == $@BG_EOSt1 )
		mapannounce "bat_a02","Netraul Flag Taken by Guillaume Team",1,0x0000FF;
	else if( getcharid(4) == $@BG_EOSt2 )
		mapannounce "bat_a02","Netraul Flag Taken by Croix Team",1,0xFF0000;
	else end;

	set getvariableofnpc(.Flag_Status,"Neutral_Flag"), 1;
	set getvariableofnpc(.Flag_Carrier,"Neutral_Flag"), getcharid(0);

	sc_end SC_HIDING;
	sc_end SC_CLOAKING;
	sc_end SC_CHASEWALK;

	pcblock 1,1; // Block Skills/Items

	stopnpctimer "Neutral_Flag";
	disablenpc "Neutral_Flag";

	addtimer 2000, "BG_EOS::OnFlash";
	end;
}

// Battleground Flag
// *********************************************************************

bat_a02,273,204,0	script	Neutral Flag::Neutral_Flag	1911,1,1,{
	end;

OnTouch:
	if( getvariableofnpc(.BG_Status,"BG_EOS") != 1 || Hp < 1 || .Flag_Status )
		end;

	if( getcharid(4) == $@BG_EOSt1 )
		mapannounce "bat_a02","Neutral Flag Taken by Guillaume Team",1,0x0000FF;
	else if( getcharid(4) == $@BG_EOSt2 )
		mapannounce "bat_a02","Neutral Flag Taken by Croix Team",1,0xFF0000;
	else end;

	set .Flag_Status, 1;
	set .Flag_Carrier, getcharid(0);

	sc_end SC_HIDING;
	sc_end SC_CLOAKING;
	sc_end SC_CHASEWALK;

	pcblock 1,1; // Block Skills/Items

	disablenpc "Neutral_Flag";
	addtimer 2000, "BG_EOS::OnFlash";
	stopnpctimer;
	end;

OnTimer2000:
	stopnpctimer;
	if( .Flag_Status == 0 && getvariableofnpc(.BG_Status,"BG_EOS") == 1 )
	{
		getmapxy .@m$, .@x, .@y, 1;
		viewpointmap "bat_a02",1,.@x,.@y,3,0xFFFF00;
		specialeffect 223;
		initnpctimer;
	}
	end;

OnDisable:
	movenpc "Neutral_Flag",273,204;
	set .Flag_Status, 0;
	stopnpctimer;
	disablenpc "Neutral_Flag";
	killmonster "bat_a02","BG_EOS::OnBreak";
	end;

OnBase:
	if( getvariableofnpc(.BG_Status,"BG_EOS") != 1 )
		end;

	movenpc "Neutral_Flag",273,204;
	set .Flag_Status, 0;
	set .Flag_Carrier, 0;
	initnpctimer;
	disablenpc "Neutral_Flag";
	mapannounce "bat_a02","The Neutral Flag have been set!",1,0x00FF00;
	bg_monster 0,"bat_a02",273,204,"Neutral Flag",1911,"BG_EOS::OnBreak";
	end;
}

// Battleground Bases
// *********************************************************************

bat_a02,173,345,0	script	North Base::North_Base	1911,3,3,{
	end;

OnTouch:
	if( getvariableofnpc(.BG_Status,"BG_EOS") != 1 || .Owner == 0 || .Owner != getcharid(4) || getvariableofnpc(.Flag_Carrier,"Neutral_Flag") != getcharid(0) )
		end;

	set getvariableofnpc(.Flag_Carrier,"Neutral_Flag"), 0;
	pcblock 1,0; // UnBlock Skills/Items
	if( .Owner == $@BG_EOSt1 )
		donpcevent "BG_EOS::OnGuillaumeFlag";
	else
		donpcevent "BG_EOS::OnCroixFlag";
	end;

OnBase:
	set .Owner, 0;
	set .Balance, 0;
	set .Tick, 0;
	setnpcdisplay "North_Base","North Base",1911;
	initnpctimer;
	end;

OnTimer1000:
	stopnpctimer;
	if( getvariableofnpc(.BG_Status,"BG_EOS") != 1 )
		end;

	set .@Guillaume, bg_getareausers($@BG_EOSt1,"bat_a02",136,329,186,361);
	set .@Croix, bg_getareausers($@BG_EOSt2,"bat_a02",136,329,186,361);
	set .Balance, .Balance + set(.@Balance, .@Guillaume - .@Croix);

	if( .Balance < -50 )
		set .Balance, -50;
	else if( .Balance > 50 )
		set .Balance, 50;

	if( .Owner == 0 )
	{
		if( .Balance == 50 )
		{
			set .Balance, 50;
			set .Owner, $@BG_EOSt1; // Guillaume
			setnpcdisplay "North_Base","Guillaume Base",1912;
			mapannounce "bat_a02","North Base captured by Guillaume Team",1,0x0000FF;
			donpcevent "GEoSNBW::OnEnable";
		}
		else if( .Balance == -50 )
		{
			set .Balance, -50;
			set .Owner, $@BG_EOSt2; // Croix
			setnpcdisplay "North_Base","Croix Base",1913;
			mapannounce "bat_a02","North Base captured by Croix Team",1,0xFF0000;
			donpcevent "CEoSNBW::OnEnable";
		}
	}
	else if( .Owner == $@BG_EOSt1 )
	{
		if( .Balance <= 0 )
		{ // Guillaume lost Control
			set .Owner, 0;
			setnpcdisplay "North_Base","North Base",1911;
			mapannounce "bat_a02","Guillaume Team lost control of the North Base",1,0x0000FF;
			donpcevent "GEoSNBW::OnDisable";
		}
	}
	else if( .Owner == $@BG_EOSt2 )
	{
		if( .Balance >= 0 )
		{ // Croix lost Control
			set .Owner, 0;
			setnpcdisplay "North_Base","North Base",1911;
			mapannounce "bat_a02","Croix Team lost control of the North Base",1,0xFF0000;
			donpcevent "CEoSNBW::OnDisable";
		}
	}

	if( .@Balance > 0 )
		specialeffect 236;
	else if( .@Balance < 0 )
		specialeffect 225;
	else if( .Owner == $@BG_EOSt1 )
		specialeffect 236;
	else if( .Owner == $@BG_EOSt2 )
		specialeffect 225;
	else
		specialeffect 223;
	
	if( .Owner == 0 )
		viewpointmap "bat_a02",1, 173, 345, 1, 0x00FF00;
	else if( .Owner == $@BG_EOSt1 )
		viewpointmap "bat_a02",1, 173, 345, 1, 0x0000FF;
	else if( .Owner == $@BG_EOSt2 )
		viewpointmap "bat_a02",1, 173, 345, 1, 0xFF0000;

	if( .Owner == 0 )
		set .Tick, 0;
	else if( set(.Tick, .Tick + 1) == 7 )
	{
		donpcevent "BG_EOS::OnNorthScore";
		set .Tick, 0;
	}

	initnpctimer;
	end;
}

bat_a02,164,50,0	script	South Base::South_Base	1911,3,3,{
	end;

OnTouch:
	if( getvariableofnpc(.BG_Status,"BG_EOS") != 1 || .Owner == 0 || .Owner != getcharid(4) || getvariableofnpc(.Flag_Carrier,"Neutral_Flag") != getcharid(0) )
		end;

//	bg_rankpoints "eos_flags",1;
	set getvariableofnpc(.Flag_Carrier,"Neutral_Flag"), 0;
	pcblock 1,0; // UnBlock Skills/Items
	if( .Owner == $@BG_EOSt1 )
		donpcevent "BG_EOS::OnGuillaumeFlag";
	else
		donpcevent "BG_EOS::OnCroixFlag";
	end;

OnBase:
	set .Owner, 0;
	set .Balance, 0;
	set .Tick, 0;
	setnpcdisplay "South_Base","South Base",1911;
	initnpctimer;
	end;

OnTimer1000:
	stopnpctimer;
	if( getvariableofnpc(.BG_Status,"BG_EOS") != 1 )
		end;

	set .@Guillaume, bg_getareausers($@BG_EOSt1,"bat_a02",129,34,175,65);
	set .@Croix, bg_getareausers($@BG_EOSt2,"bat_a02",129,34,175,65);
	set .Balance, .Balance + set(.@Balance, .@Guillaume - .@Croix);

	if( .Balance < -50 )
		set .Balance, -50;
	else if( .Balance > 50 )
		set .Balance, 50;

	if( .Owner == 0 )
	{
		if( .Balance == 50 )
		{
			set .Balance, 50;
			set .Owner, $@BG_EOSt1; // Guillaume
			setnpcdisplay "South_Base","Guillaume Base",1912;
			mapannounce "bat_a02","South Base captured by Guillaume Team",1,0x0000FF;
			donpcevent "GEoSSBW::OnEnable";
		}
		else if( .Balance == -50 )
		{
			set .Balance, -50;
			set .Owner, $@BG_EOSt2; // Croix
			setnpcdisplay "South_Base","Croix Base",1913;
			mapannounce "bat_a02","South Base captured by Croix Team",1,0xFF0000;
			donpcevent "CEoSSBW::OnEnable";
		}
	}
	else if( .Owner == $@BG_EOSt1 )
	{
		if( .Balance <= 0 )
		{ // Guillaume lost Control
			set .Owner, 0;
			setnpcdisplay "South_Base","North Base",1911;
			mapannounce "bat_a02","Guillaume Team lost control of the South Base",1,0x0000FF;
			donpcevent "GEoSSBW::OnDisable";
		}
	}
	else if( .Owner == $@BG_EOSt2 )
	{
		if( .Balance >= 0 )
		{ // Croix lost Control
			set .Owner, 0;
			setnpcdisplay "South_Base","North Base",1911;
			mapannounce "bat_a02","Croix Team lost control of the South Base",1,0xFF0000;
			donpcevent "CEoSSBW::OnDisable";
		}
	}

	if( .@Balance > 0 )
		specialeffect 236;
	else if( .@Balance < 0 )
		specialeffect 225;
	else if( .Owner == $@BG_EOSt1 )
		specialeffect 236;
	else if( .Owner == $@BG_EOSt2 )
		specialeffect 225;
	else
		specialeffect 223;
	
	if( .Owner == 0 )
		viewpointmap "bat_a02",1,164,50,2,0x00FF00;
	else if( .Owner == $@BG_EOSt1 )
		viewpointmap "bat_a02",1,164,50,2,0x0000FF;
	else if( .Owner == $@BG_EOSt2 )
		viewpointmap "bat_a02",1,164,50,2,0xFF0000;

	if( .Owner == 0 )
		set .Tick, 0;
	else if( set(.Tick, .Tick + 1) == 7 )
	{
		donpcevent "BG_EOS::OnSouthScore";
		set .Tick, 0;
	}

	initnpctimer;
	end;
}

// Battleground Therapist
// *********************************************************************

bat_a02,53,377,3	script	Battlefield Surgeon#eoe2	95,{
	specialeffect2 312;
	skilleffect 34,0;
	skilleffect 29,0;
	percentheal 100,100;
	sc_start SC_BLESSING,300000,10;
	sc_start SC_INC_AGI,300000,10;
	repairall;
	end;

}

bat_a02,45,18,3	script	Battlefield Surgeon#eoe1	95,{
	specialeffect2 312;
	skilleffect 34,0;
	skilleffect 29,0;
	percentheal 100,100;
	sc_start SC_BLESSING,300000,10;
	sc_start SC_INC_AGI,300000,10;
	repairall;
	end;

}

// Battleground Respawn
// *********************************************************************

bat_a02,46,377,0	script	North Base Warp::GEoSNBW	723,{
	end;

OnEnable:
	waitingroom "Join To Warp",20;
	end;

OnDisable:
	delwaitingroom;
	end;

OnWarp:
	warpwaitingpc "bat_a02",115,346,20;
	end;
}

bat_a02,53,370,0	script	South Base Warp::GEoSSBW	723,{
	end;

OnEnable:
	waitingroom "Join To Warp",20;
	end;

OnDisable:
	delwaitingroom;
	end;

OnWarp:
	warpwaitingpc "bat_a02",106,48,20;
	end;
}

// *********************************************************************

bat_a02,38,19,0	script	North Base Warp::CEoSNBW	723,{
	end;

OnEnable:
	waitingroom "Join To Warp",20;
	end;

OnDisable:
	delwaitingroom;
	end;

OnWarp:
	warpwaitingpc "bat_a02",115,346,20;
	end;
}

bat_a02,45,12,0	script	South Base Warp::CEoSSBW	723,{
	end;

OnEnable:
	waitingroom "Join To Warp",20;
	end;

OnDisable:
	delwaitingroom;
	end;

OnWarp:
	warpwaitingpc "bat_a02",106,48,20;
	end;
}

// *********************************************************************

bat_a02,50,374,0	script	#guieoe_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_a02",46,370,54,378,100,100;
	donpcevent "GEoSNBW::OnWarp";
	donpcevent "GEoSSBW::OnWarp";
	areawarp "bat_a02",46,370,54,378,"bat_a02",353,344;
	initnpctimer;
	end;
}

bat_a02,42,16,0	script	#croeoe_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_a02",38,12,47,21,100,100;
	donpcevent "CEoSNBW::OnWarp";
	donpcevent "CEoSSBW::OnWarp";
	areawarp "bat_a02",38,12,47,21,"bat_a02",353,52;
	initnpctimer;
	end;
}

// MapFlags
// *********************************************************************

bat_a02	mapflag	battleground	2
bat_a02	mapflag	nomemo
bat_a02	mapflag	nosave	SavePoint
bat_a02	mapflag	noteleport
bat_a02	mapflag	nowarp
bat_a02	mapflag	nowarpto
bat_a02	mapflag	noreturn
bat_a02	mapflag	nobranch
bat_a02	mapflag	nopenalty
bat_a02	mapflag	loadevent

// Eye of the Storm Effects
// *********************************************************************

bat_a02,269,189,0	script	#stormef1	139,{
	end;

OnStartStorm:
	if( getvariableofnpc(.BG_Status,"BG_EOS") != 1 ) end;

	sleep (rand(15,40) * 1000);
	specialeffect 622;
	specialeffect 537;

	goto OnStartStorm;
	end;
}

bat_a02,284,189,0	duplicate(#stormef1)	#stormef2	139
bat_a02,267,204,0	duplicate(#stormef1)	#stormef3	139
bat_a02,278,210,0	duplicate(#stormef1)	#stormef4	139
bat_a02,262,210,0	duplicate(#stormef1)	#stormef5	139
bat_a02,256,201,0	duplicate(#stormef1)	#stormef6	139
bat_a02,284,218,0	duplicate(#stormef1)	#stormef7	139
bat_a02,263,220,0	duplicate(#stormef1)	#stormef8	139
bat_a02,289,207,0	duplicate(#stormef1)	#stormef9	139
bat_a02,279,182,0	duplicate(#stormef1)	#stormef10	139
bat_a02,272,229,0	duplicate(#stormef1)	#stormef11	139